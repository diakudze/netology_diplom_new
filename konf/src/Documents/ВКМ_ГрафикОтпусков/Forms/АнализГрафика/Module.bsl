#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресХранилища = Параметры.АдресХранилища;  
	
	ТЗ_Отпуск = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	ЗаполнитьДиаграммуГанта(ТЗ_Отпуск);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДиаграммуГанта(ТЗ_Данные)
	
	ДГ_Элемент = Элементы.ДиаграммаГанта; 
	
	// Установить заголовок дидаграммы. 
	ДГ_Элемент.Заголовок = "График отпусков";
	ТЗ_Сотрудники = ТЗ_Данные.Скопировать(,"Сотрудник");
	ТЗ_Сотрудники.Сортировать("Сотрудник",);
	ТЗ_Сотрудники.Свернуть("Сотрудник",); 
	
	
	Серия = ДиаграммаГанта.Серии.Добавить();        
	Серия.Текст = "Оплачиваемый отпуск.";
	Серия.Цвет = WebЦвета.Бирюзовый;
	Серия.ДополнительныйЦвет = WebЦвета.Красный;
	
	ТочкаГод = ДиаграммаГанта.Точки.Добавить();
	ТочкаГод.Текст = "2024";
	
	Для Каждого СтрокаТЗ_Сотрудник Из ТЗ_Сотрудники Цикл
		
		ТочкаСотрудник = ТочкаГод.Точки.Добавить();
		ТочкаСотрудник.Значение = СтрокаТЗ_Сотрудник.Сотрудник;
		ТочкаСотрудник.Текст = СтрокаТЗ_Сотрудник.Сотрудник;	
		
		Отбор = Новый Структура;
		Отбор.Вставить("Сотрудник", СтрокаТЗ_Сотрудник.Сотрудник);
		НайденныеСтроки = ТЗ_Данные.НайтиСтроки(Отбор);
		
		Счетчик = 0;
		Для Каждого СтрокаПериодОтпуска Из НайденныеСтроки Цикл
			
			Счетчик = Счетчик + 1;
			ТочкаПериодОтпуска = ТочкаСотрудник.Точки.Добавить();
			ТочкаПериодОтпуска.Значение = (Строка(СтрокаТЗ_Сотрудник) + "_" + Строка(СтрокаПериодОтпуска.ИсходныйНомерСтроки));
			ТочкаПериодОтпуска.Текст = Строка(Счетчик) + " период. Дней отпуска: " + Строка(СтрокаПериодОтпуска.КолВоДней);
			
			ЗначениеГод = ДиаграммаГанта.ПолучитьЗначение(ТочкаГод, Серия);
			ЗначениеСотрудник = ДиаграммаГанта.ПолучитьЗначение(ТочкаСотрудник, Серия);
			Значение = ДиаграммаГанта.ПолучитьЗначение(ТочкаПериодОтпуска, Серия);
			
			ИнтервалГод = ЗначениеГод.Добавить();
			ИнтервалГод.Начало = СтрокаПериодОтпуска.ДатаНачала;
			ИнтервалГод.Конец = СтрокаПериодОтпуска.ДатаОкончания;   
			ИнтервалГод.Цвет = WebЦвета.ЗеленоватоЛимонный;
			
			ИнтервалСотрудник = ЗначениеСотрудник.Добавить();
			ИнтервалСотрудник.Начало = СтрокаПериодОтпуска.ДатаНачала;
			ИнтервалСотрудник.Конец = СтрокаПериодОтпуска.ДатаОкончания;
			ИнтервалСотрудник.Цвет = WebЦвета.Бирюзовый;
			ИнтервалСотрудник.Текст = "с " + Строка(Формат(СтрокаПериодОтпуска.ДатаНачала, "ДЛФ=DD")) + " по " + Строка(Формат(СтрокаПериодОтпуска.ДатаОкончания, "ДЛФ=DD"));
			
			Интервал = Значение.Добавить();
			Интервал.Начало = СтрокаПериодОтпуска.ДатаНачала;
			Интервал.Конец = СтрокаПериодОтпуска.ДатаОкончания;
			Интервал.Цвет = WebЦвета.БледноБирюзовый;
			Интервал.Текст = "с " + Строка(Формат(СтрокаПериодОтпуска.ДатаНачала, "ДЛФ=DD")) + " по " + Строка(Формат(СтрокаПериодОтпуска.ДатаОкончания, "ДЛФ=DD"));
			
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры;

#КонецОбласти

