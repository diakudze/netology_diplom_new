#Область СлужебныеПроцедурыиФункции

Процедура ОтправитьСообщения() Экспорт
	
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_УведомленияТелеграмБоту.Ссылка КАК Ссылка,
	|	ВКМ_УведомленияТелеграмБоту.ТекстСообщения КАК ТекстСообщения,
	|	ЛОЖЬ КАК СообщениеДоставлено
	|ИЗ
	|	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту
	|ГДЕ
	|	НЕ ВКМ_УведомленияТелеграмБоту.ПометкаУдаления";
	
	ТЗ_ТекстСообщений = Запрос.Выполнить().Выгрузить();
	
	Если ЗначениеЗаполнено(ТЗ_ТекстСообщений) Тогда
		ТЗ_ОбработанныеСообщения = ОтправитьСообщениеВТелеграмКанал(ТЗ_ТекстСообщений);
		
		//Удаляем успешно отправленные сообщения
		Отбор = Новый Структура();
		Отбор.Вставить("СообщениеДоставлено", Истина);
		
		НайденныеСтроки = ТЗ_ОбработанныеСообщения.НайтиСтроки(Отбор);
		
		Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
			
			Для Каждого СтрокаТЗ Из НайденныеСтроки Цикл
				
				СпрОбъект = СтрокаТЗ.Ссылка.ПолучитьОбъект();
				СпрОбъект.Удалить()				
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕСли;
	
КонецПроцедуры

Функция ОтправитьСообщениеВТелеграмКанал(ТЗ_Данные) Экспорт
	
	ТексСообщения = "Ответ HTTP: | ";
	Событие = "(ИНТЕГРАЦИЯ) Телеграм бот";  
	
	Токен = Константы.ВКМ_ТелеграммТокен.Получить();
	СерверТелеграм = "api.telegram.org"; 
	Чат_ID = Константы.ВКМ_ID_Чата.Получить(); 
	
	Ресурс = "bot" + Токен + "/sendMessage";	
	
	Соединение  =  Новый HTTPСоединение(СерверТелеграм,443,,,,,Новый ЗащищенноеСоединениеOpenSSL());
	
	Для Каждого СтрокаТЗ Из ТЗ_Данные Цикл 
		
		СтруктураДанныхСообщения = Новый Структура;
		СтруктураДанныхСообщения.Вставить("chat_id", СтрЗаменить(Формат(Чат_ID, "ЧДЦ=; ЧС=; ЧРГ=."), ".", ""));
		СтруктураДанныхСообщения.Вставить("text", СтрокаТЗ.ТекстСообщения);
		
		Запрос = Новый HTTPЗапрос(Ресурс);
		Запрос.Заголовки.Вставить("Content-Type",  ТипКонтентаJSON());
		Запрос.УстановитьТелоИзСтроки(СтрокаJSON(СтруктураДанныхСообщения));
		
		Ответ = Соединение.ВызватьHTTPМетод("POST",Запрос);	
		
		Если Ответ.КодСостояния = 200 Тогда
			СтрокаТЗ.СообщениеДоставлено = Истина;
		Иначе
			СтрокаОтвета = ОбъектJSON(Ответ.ПолучитьТелоКакСтроку());
			Если ЗначениеЗаполнено(СтрокаОтвета) Тогда
				Для Каждого Элемент Из СтрокаОтвета Цикл
					ТексСообщения = ТексСообщения + Элемент.Ключ + " : " + Элемент.Значение + " | "; 			
				КонецЦикла;	
				ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка, Метаданные.ОбщиеМодули.ВКМ_Телеграм,,ТексСообщения,);
			КонецЕсли;		
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТЗ_Данные;  
	
КонецФункции 

Функция СтрокаJSON(ОбъектJSON) Экспорт 
	
	ПараметыЗаписи = Новый ПараметрыЗаписиJSON(, Символы.Таб);  
	
	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку(ПараметыЗаписи);
	ЗаписатьJSON(Запись, ОбъектJSON);   
	
	Возврат Запись.Закрыть();
	
КонецФункции  

Функция ОбъектJSON(СтрокаJSON) Экспорт 
	
	Чтение = Новый ЧтениеJSON;
	Чтение.УстановитьСтроку(СтрокаJSON);
	ОбъектJSON = ПрочитатьJSON(Чтение);
	Чтение.Закрыть();
	
	Возврат ОбъектJSON;
	
КонецФункции 

Функция ОтветJSON(Объект, КодСостояния = 200) Экспорт
	
	Ответ = Новый HTTPСервисОтвет(КодСостояния,,);
	Ответ.УстановитьТелоИзСтроки(СтрокаJSON(Объект));
	Ответ.Заголовки.Вставить("Content-Type",  ТипКонтентаJSON());
	
	Возврат Ответ;
	
КонецФункции 


Функция ТипКонтентаJSON()
	Возврат "application/json"
КонецФункции

#КонецОбласти